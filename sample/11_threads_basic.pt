// 11 - Threads (Basic)
// Functions run concurrently inside a "multi" block using "thread".
// They are pure: can read globals (snapshot), cannot write globals.

// Define functions normally with the "function" keyword
function worker(name, steps) do
    i = 0
    loop i < steps infinite do
        PRINT(name + " step " + TO_STRING(i + 1))
        i = i + 1
    end
    return name + " done"
end

// Run functions in parallel with "multi result do ... end"
// Use "thread funcCall() -> key" to name each result in the collection
multi result do
    thread worker("Alpha", 3) -> resultA
    thread worker("Beta", 3) -> resultB
end

// Results are available after the multi block
// Each entry is {status: "done", ok: true, value: <return value>}
PRINT("---")
PRINT("Alpha status:", result.resultA.status)
PRINT("Alpha returned:", result.resultA.value)
PRINT("Beta returned:", result.resultB.value)
